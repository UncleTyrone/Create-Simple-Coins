plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.134'
}

version = project.mod_version
group = project.mod_group_id

base {
    archivesName = project.mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = "https://maven.createmod.net" }
    maven { url = "https://maven.ithundxr.dev/snapshots" }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

neoForge {
    version = project.neo_version
    parchment {
        minecraftVersion = project.minecraft_version
        mappingsVersion = project.parchment_mappings
    }

    runs {
        client {
            client()
        }
        server {
            server()
        }
        gameTestServer {
            type = 'gameTestServer'
        }
        data {
            data()
        }
    }

    mods {
        "${project.mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

neoForge.addModdingDependenciesTo(sourceSets.main)

dependencies {
    implementation("com.simibubi.create:create-${project.minecraft_version}:${project.create_version}:slim") {
        transitive = false
    }
    implementation("net.createmod.ponder:Ponder-NeoForge-${project.minecraft_version}:${project.ponder_version}")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${project.minecraft_version}:${project.flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${project.minecraft_version}:${project.flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${project.registrate_version}")

    annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixin_extras}")
    implementation("io.github.llamalad7:mixinextras-neoforge:${project.mixin_extras}")

    compileOnly("mezz.jei:jei-${project.minecraft_version}-common-api:${project.jei_version}")
    compileOnly("mezz.jei:jei-${project.minecraft_version}-neoforge-api:${project.jei_version}")
    runtimeOnly("mezz.jei:jei-${project.minecraft_version}-neoforge:${project.jei_version}")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            version              : project.version,
            mod_id               : project.mod_id,
            minecraft_version    : project.minecraft_version,
            minecraft_version_range: project.minecraft_version_range,
            neo_version          : project.neo_version,
            neo_version_range    : project.neo_version_range,
            loader_version_range : project.loader_version_range,
            mod_version          : project.mod_version,
            mod_name             : project.mod_name,
            mod_license          : project.mod_license,
            mod_authors          : project.mod_authors,
            mod_description      : project.mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand(replaceProperties)
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
    manifest {
        attributes([
                "Specification-Title"   : project.mod_id,
                "Specification-Vendor"  : project.mod_authors,
                "Specification-Version" : "1",
                "Implementation-Title"  : project.mod_id,
                "Implementation-Version": project.mod_version,
                "Implementation-Vendor" : project.mod_authors
        ])
    }
}

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing:
// tasks.named('publish').configure {
//     dependsOn 'reobfJar'
// }

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.mod_id
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}
